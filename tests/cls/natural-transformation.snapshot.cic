@class Category [
  Object: Type
  Morphism(dom cod: Object): Type
  id(x: Object): Morphism(x, x)

  compose(
    x y z: Object,
    f: Morphism(x, y),
    g: Morphism(y, z),
  ): Morphism(x, z)

  id_left(
    x y: Object,
    f: Morphism(x, y)
  ): Equal(Morphism(x, y), compose(x, x, y)(id(x), f), f)

  id_right(
    x y: Object,
    f: Morphism(x, y),
  ): Equal(Morphism(x, y), compose(x, y, y)(f, id(y)), f)

  compose_associative(
    x y z w: Object,
    f: Morphism(x, y),
    g: Morphism(y, z),
    h: Morphism(z, w),
  ): Equal(
    Morphism(x, w),
    compose(x, y, w)(f, compose(y, z, w)(g, h)),
    compose(x, z, w)(compose(x, y, z)(f, g), h))
]

@class Functor [
  dom: Category
  cod: Category
  map(x: dom.Object): cod.Object

  fmap(
    x y: dom.Object,
    f: dom.Morphism(x, y)
  ): cod.Morphism(map(x), map(y))

  fmap_respect_compose(
    x y z: dom.Object,
    f: dom.Morphism(x, y),
    g: dom.Morphism(y, z),
  ): Equal(
    cod.Morphism(map(x), map(z)),
    fmap(x, z)(dom.compose(x, y, z)(f, g)),
    cod.compose(map(x), map(y), map(z))(fmap(x, y)(f), fmap(y, z)(g)))

  fmap_respect_id(
    x: dom.Object
  ): Equal(
    cod.Morphism(map(x), map(x)),
    fmap(x, x)(dom.id(x)),
    cod.id(map(x)))
]

@class NaturalTransformation [
  dom: Category
  cod: Category
  src: Functor(dom, cod)
  tar: Functor(dom, cod)
  component(x: dom.Object): cod.Morphism(src.map(x), tar.map(x))

  naturality(
    x y: dom.Object,
    f: dom.Morphism(x, y),
  ): Equal(
    cod.Morphism(src.map(x), tar.map(y)),
    cod.compose(src.map(x), tar.map(x), tar.map(y))(component(x), tar.fmap(x, y)(f)),
    cod.compose(src.map(x), src.map(y), tar.map(y))(src.fmap(x, y)(f), component(y)))
]

@show NaturalTransformation

@class NaturalTransformation2 [
  dom: Category
  cod: Category
  src: Functor(dom, cod)
  tar: Functor(dom, cod)
  // NOTE `src.dom.Object` is the same as `dom.Object`
  component(x: src.dom.Object): cod.Morphism(src.map(x), tar.map(x))

  naturality(
    x y: dom.Object,
    f: dom.Morphism(x, y),
  ): Equal(
    cod.Morphism(src.map(x), tar.map(y)),
    cod.compose(src.map(x), tar.map(x), tar.map(y))(component(x), tar.fmap(x, y)(f)),
    cod.compose(src.map(x), src.map(y), tar.map(y))(src.fmap(x, y)(f), component(y)))
]

@show NaturalTransformation2

@show
@the Equal(
  Type,
  NaturalTransformation,
  NaturalTransformation2)
same
