# 13. Even Haf a Baker's Dozen

## Use `Either` to express "or" as a type

``` cicada
the(Type, Either(Nat, String))
the(Either(Nat, String), inl(6))
the(Either(Nat, String), inr("abc"))
```

Let's use define a useless `size` to test `either_ind`.

``` cicada
import { length } from "ch05.snapshot.cic"

def size(either: Either(Nat, List(String))): Nat =
  either_ind(
    either,
    (_) => Nat,
    (n) => n,
    (list) => length(String, list))

size(inl(3))
size(inr(li! ["a", "b", "c"]))
```

## `even_or_odd`

Every natural number is even or odd.

``` cicada
import {
  even_t,
  odd_t,
  zero_is_even,
  // one_is_odd,
  even_add1_is_odd,
  odd_add1_is_even,
} from "ch12.snapshot.md"


def even_or_odd(n: Nat): Either(even_t(n), odd_t(n)) =
  let motive(target: Nat): Type = Either(even_t(target), odd_t(target))

  let base: Either(even_t(0), odd_t(0)) = inl(zero_is_even)

  let step(
    prev: Nat,
    almost: Either(even_t(prev), odd_t(prev))
  ): Either(even_t(add1(prev)), odd_t(add1(prev))) =
    either_ind(
      almost,
      (_) => Either(even_t(add1(prev)), odd_t(add1(prev))),
      (even) => inr(even_add1_is_odd(prev, even)),
      (odd) => inl(odd_add1_is_even(prev, odd)))

  nat_ind(n, motive, base, step)
```

Use "same-as" chart to find out the following values.

At each step, look for the parts of expressions that change and those that don't.
Try to identify motives, bases, and steps that appear multiple times.

``` cicada
even_or_odd(0)
even_or_odd(1)
even_or_odd(2)
even_or_odd(3)
even_or_odd(4)
even_or_odd(5)
even_or_odd(6)
even_or_odd(7)
```
