# 12. Even Numbers Can Be Odd

## `Even`

``` cicada
import { double } from "ch09.snapshot.cic"

def Even(n: Nat): Type {
  (half: Nat) * Equal(Nat, n, double(half))
}

def ten_is_even: Even(10) = cons(5, same)
def zero_is_even: Even(0) = cons(0, same)
```

Although two functions always return the same answer,
sometimes one of them is easier to use
because it more quickly becomes a value.

In particular, `add` and thus `twice`
leave an `add1` on the second argument,
while `double` puts both `add1`s at the top immediately.

**Carefully Choose Definitions**

Carefully-chosen definitions can greatly simplify later proofs.

## `even_add_2_is_even`

``` cicada
import { add } from "ch03.snapshot.cic"
import { cong } from "ch08.snapshot.cic"

def even_add_2_is_even(n: Nat, even: Even(n)): Even(add(2, n)) {
  let half = car(even)
  let almost = cdr(even)
  cons(add1(half), cong(Nat, Nat, n, double(half), almost, add(2)))
}

def two_is_even: Even(2) = even_add_2_is_even(0, zero_is_even)
```

## `Odd`

``` cicada
def Odd(n: Nat): Type {
  // `haf` is the half of the even number, that is one smaller than `n`.
  (haf: Nat) * Equal(Nat, n, add1(double(haf)))
}

def eleven_is_odd: Odd(11) = cons(5, same)
def one_is_odd: Odd(1) = cons(0, same)
```

## `even_add1_is_odd`

``` cicada
def even_add1_is_odd(n: Nat, even: Even(n)): Odd(add1(n)) {
  let half = car(even)
  let almost = cdr(even)
  cons(half, cong(Nat, Nat, n, double(half), almost, add(1)))
}
```

## `odd_add1_is_even`

``` cicada
def odd_add1_is_even(n: Nat, odd: Odd(n)): Even(add1(n)) {
  let haf = car(odd)
  let almost = cdr(odd)
  cons(add1(haf), cong(Nat, Nat, n, add1(double(haf)), almost, add(1)))
}
```

## Behold! Ackermann!

``` cicada
def repeat(f: (Nat) -> Nat, n: Nat): Nat {
  nat_rec(n, f(1), (_prev, almost) => f(almost))
}

def ackermann(n: Nat): (Nat) -> Nat {
  nat_rec(n, add(1), (_prev, almost) => repeat(almost))
}

ackermann(0, 0)
ackermann(1, 0)
ackermann(2, 0)
ackermann(3, 0)

ackermann(0, 1)
ackermann(1, 1)
ackermann(2, 1)
ackermann(3, 1)

ackermann(0, 2)
ackermann(1, 2)
ackermann(2, 2)
ackermann(3, 2)

ackermann(0, 3)
ackermann(1, 3)
ackermann(2, 3)
ackermann(3, 3)
```
