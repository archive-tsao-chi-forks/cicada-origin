@datatype Nat: Type {
  zero: Nat
  add1: (prev: Nat) -> Nat
}

zero = Nat.zero
one = Nat.add1(zero)
two = Nat.add1(one)
three = Nat.add1(two)
four = Nat.add1(three)
five = Nat.add1(four)
six = Nat.add1(five)
seven = Nat.add1(six)
eight = Nat.add1(seven)
nine = Nat.add1(eight)
ten = Nat.add1(nine)

Nat_repr : (Nat) -> String
Nat_repr = {
  (Nat.add1(Nat.add1(Nat.add1(Nat.add1(Nat.add1(Nat.add1(Nat.add1(_)))))))) => "big"
  (Nat.add1(Nat.add1(Nat.add1(Nat.add1(Nat.add1(Nat.add1(Nat.zero))))))) => "six"
  (Nat.add1(Nat.add1(Nat.add1(Nat.add1(Nat.add1(Nat.zero)))))) => "five"
  (Nat.add1(Nat.add1(Nat.add1(Nat.add1(Nat.zero))))) => "four"
  (Nat.add1(Nat.add1(Nat.add1(Nat.zero)))) => "three"
  (Nat.add1(Nat.add1(Nat.zero))) => "two"
  (Nat.add1(Nat.zero)) => "one"
  (Nat.zero) => "zero"
}

@show Nat_repr(zero)
@show Nat_repr(one)
@show Nat_repr(two)
@show Nat_repr(three)
@show Nat_repr(four)
@show Nat_repr(five)
@show Nat_repr(six)
@show Nat_repr(seven)
@show Nat_repr(eight)

@datatype Bool : Type {
  true: Bool
  false: Bool
}

Nat_ind : (target: Nat) -> (motive: (target: Nat) -> Type) ->
  (base: motive(Nat.zero)) ->
  (step: (prev: Nat) -> (almost: motive(prev)) -> motive(Nat.add1(prev))) ->
  motive(target)
Nat_ind = {
  (Nat.zero) => (motive) => (base) => (step) => base
  (Nat.add1(prev)) => (motive) => (base) => (step) =>
    step(prev)(Nat_ind(prev)(motive)(base)(step))
}

Nat_rec : (target: Nat) -> (T: Type) -> (base: T) -> (step: (prev: Nat) -> (almost: T) -> T) -> T
Nat_rec = (target) => (T) => (base) => (step) => Nat_ind(target)((_target) => T)(base)(step)

Nat_which : (target: Nat) -> (T: Type) -> (base: T) -> (step: (prev: Nat) -> T) -> T
Nat_which = (target) => (T) => (base) => (step) =>
  Nat_rec(target)(T)(base)((prev) => (_almost) => step(prev))

Nat_iter : (target: Nat) -> (T: Type) -> (base: T) -> (step: (almost: T) -> T) -> T
Nat_iter = (target) => (T) => (base) => (step) =>
  Nat_rec(target)(T)(base)((_prev) => (almost) => step(almost))

add : (Nat) -> (Nat) -> Nat
add = (x) => (y) => Nat_iter(y)(Nat)(x)((almost) => Nat.add1(almost))

@show add(zero)(zero)
@show add(zero)(one)
@show add(one)(zero)
@show add(one)(one)
@show add(one)(two)
@show add(two)(one)
@show add(two)(two)

mul : (Nat) -> (Nat) -> Nat
mul = (x) => (y) => Nat_iter(y)(Nat)(zero)((almost) => add(almost)(x))

pow : (Nat) -> (Nat) -> Nat
pow = (x) => (y) => Nat_iter(y)(Nat)(one)((almost) => mul(almost)(x))

eight_again = @begin {
  four = add(two)(two)
  add(four)(four)
}

@show "eight_again"
@show eight_again

@show "add(four)(three)"
@show add(four)(three)
@show "mul(four)(three)"
@show mul(four)(three)
@show "pow(four)(three)"
@show pow(four)(three)

gauss : (Nat) -> Nat
gauss = (n) => Nat_rec(n)(Nat)(Nat.zero)((prev) => (almost) => add(Nat.add1(prev))(almost))

@show gauss(zero)
@show gauss(one)
@show gauss(two)
@show gauss(three)
@show gauss(four)
@show gauss(five)
@show gauss(six)
